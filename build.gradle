// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.31'
    ext.mlkit_version = '16.0.0'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath 'com.google.gms:google-services:4.2.0'
    }
}
plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
}
allprojects {
    repositories {
        google()
        mavenLocal()
        jcenter()
        maven { url "https://jitpack.io" }
    }
    // See: https://github.com/ben-manes/gradle-versions-plugin
    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}
ext {
    retrofitVersion = '2.3.0'
    okhttpVersion = '3.9.0'
    supportLibVersion = '26.1.0'
    permissionsDispatcherVersion = '2.3.1'
    butterKnifeVersion = '9.0.0-rc2'
    stethoVersion = '1.5.0'
    debugdrawerVersion = '0.7.0'
    androidJunitRunnerVersion = '1.0.1'
    espressoVersion = '3.0.1'
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.31.0"
}

task("ktlint", type: JavaExec, group: "verification") {
    def outputDir = "${project.buildDir}/reports/ktlint/"
    def inputFiles = project.fileTree(dir: "src", include: "**/*.kt")
    def outputFile = "${outputDir}ktlint-checkstyle-report.xml"

    // See:
    // https://medium.com/@vanniktech/making-your-gradle-tasks-incremental-7f26e4ef09c3
    inputs.files(inputFiles)
    outputs.dir(outputDir)

    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args = [
        "--format",
        "--android",
        "--reporter=plain",
        "--reporter=checkstyle,output=${outputFile}",
        "**/*.kt",
    ]
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
